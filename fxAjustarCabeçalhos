let

 funcao =
    (
      Tabela as table,
      Separador as text,
      LetraMaiuscula as text
    )=>

    let
      
      /*
      Fonte = Excel.CurrentWorkbook(){[Name = "Tabela1"]}[Content],
      Separador = "_",
      LetraMaiuscula = "sim",
      */

      Fonte = Tabela,
      ColunasNomes = Table.FromList(Table.ColumnNames(Fonte)),
      NovosNomesCriados = 
        Table.AddColumn
        (ColunasNomes, 
        "Personalizar", 
        each {
          [Column1],
          if 
          Text.Upper(LetraMaiuscula) = "SIM"
          then Text.Combine(Text.Split(Text.Upper([Column1])," "),Separador)
          else Text.Combine(Text.Split(Text.Proper([Column1])," "),Separador)
        }
      )[Personalizar],
        Final = Table.RenameColumns(
        Fonte,
        NovosNomesCriados
    )
    in
        Final,

            metadados = [
                Documentation.Name  = "Função para alterar os nomes dos cabeçalhos",
                Documentation.Description ="
                Pega uma tabela e a partir dos parâmetros passados renomeia as colunas alterando os nomes para letra maisucula ou separando as palavras através de um delimitador
                ",
                Documentation.Examples = {

                    // exemplo 01
                    [
                        Description = "descricao do exemplo 1",
                        Code = "
                        funcao(
                          ---------------------------------
                          |data de nascimento|nome completo|
                          |30/10/1999        |lucas ribeiro|
                          ---------------------------------
                          ,""_"",

                          ""sim""
                        )
                          ",
                        Result = " 
                          ---------------------------------                    
                          |DATA_DE_NASCIMENTO|NOME_COMPLETO|
                          |30/10/1999        |lucas ribeiro|
                           ---------------------------------
                        "
                    ],

                    // exemplo 02
                    [
                        Description = "descricao do exemplo 2",
                      Code = "
                        funcao(
                          ---------------------------------
                          |data de nascimento|nome completo|
                          |30/10/1999        |lucas ribeiro|
                          ---------------------------------
                          ,""--"",

                          ""não""
                        )
                          ",
                        Result = "      
                           ----------------------------------
                          |Data--De--Nascimento|Nome--Completo|
                          |30/10/1999          |lucas ribeiro |
                           ----------------------------------
                        "
                    ]                    
                }
            ]
in

    Value.ReplaceType(
        funcao,
        Value.ReplaceMetadata(
            Value.Type(funcao),metadados
        )
    )
