(
    TabelaDe as table,
    ColunaJoinDe as text,
    ColunaDe as text,
    TabelaPara as table,
    ColunaJoinPara as text,
    ColunaPara as text,
    TipoColuna as type
)=>


let


/*-----------VARIAVEIS PARA AUDITORIA------------

    //Fonte = Excel.Workbook(File.Contents("C:\Users\user\OneDrive\Área de Trabalho\Teste Funcao\CONTROLE DE ENTREGAS NOVO - Copia.xlsm"), null, true),
    TabelaDe = DE,
    ColunaJoinDe = "NOME",
    ColunaDe = "IDADE",
    TabelaPara = PARA,
    ColunaJoin = "NOME",
    ColunaPara = "IDADE",
    ColunaJoinPara = "NOME",
    TipoColuna = type number,
*/


    /*--------------------INICIO FUNCAO---------------*/
    NomeColuna = "Lixo",
    MeclaDeConsultas = //MESCLANDO AS TABELAS A E B
        Table.NestedJoin(
            TabelaDe, 
            {""& ColunaJoinDe &""}, 
            TabelaPara, 
            {""& ColunaJoinPara &""}, 
            ""& NomeColuna &"", 
            JoinKind.LeftOuter
        ),
    MesclaExpandida = // EXPANDINDO DA TABELA B APENAS A COLUNA QUE OS VALORES SERVIRÃO DE BASE DE SUBSTITUÇÃO,
        Table.ExpandTableColumn(
            MeclaDeConsultas, 
            NomeColuna, 
            {"" &ColunaPara& ""}, 
            {"" &NomeColuna& ""}
        ), 
    Substituicao = // REALIZA A SUBSTITUIÇÃO DE VALORES DA COLUNA SELECIONADA DA TABELA A, COM BASE NA COLUNA REFERENCIA DA TABELA B
        Table.ReplaceValue(
            MesclaExpandida,
            each Record.Field(_, ColunaDe),
            each
                if Record.Field(_, NomeColuna) = null then
                Record.Field(_, ColunaDe)
                else
                Record.Field(_, NomeColuna),
            Replacer.ReplaceValue,
            {"" & ColunaDe & ""}
        ),
    ColunasRemovidas = Table.RemoveColumns(Substituicao, {""&NomeColuna&""}),
    TipagemColuna = Table.TransformColumnTypes(ColunasRemovidas, {{ColunaDe, TipoColuna }})

in
    TipagemColuna




    /* ESSA FUNÇÃO FAZ UMA SUBSTUIÇAO DOS VALORES DE UMA COLUNA COM BASE NOS VALORES DE OUTRA, E ESSA COLUNA QUE SERÁ A "COLUNA PARA"
        PODE ESTAR EM OUTRA TABELA, POIS ESSA FUNÇÃO FAZ UM JOIN ENTRE TABLEAS*/
