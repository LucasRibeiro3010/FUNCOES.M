let
  ListaDatas = (DataIni as date, DataFim as date) =>
    List.Dates(DataIni, Duration.Days(DataFim - DataIni) + 1, #duration(1, 0, 0, 0)),
  _ini = #date(2022, 01, 01),
  _fim = #date(2024, 12, 31),
  Fonte = ListaDatas(_ini, _fim),
  ListaParaTabela = Table.FromList(Fonte, Splitter.SplitByNothing(), null, null, ExtraValues.Error),
  #"Tipo Alterado" = Table.TransformColumnTypes(ListaParaTabela, {{"Column1", type date}}),
  Tabela = Table.AddColumn(
    #"Tipo Alterado",
    "Outros",
    each Table.FromRecords(
      {
        [
          DATA_ID = Date.ToText([Column1], "ddMMyyyy"),
          DIA = Date.Day([Column1]),
          ANO = Date.Year([Column1]),
          ANO_MÊS = Date.ToText([Column1], "yyyy-MM"),
          BIMESTRE = Number.RoundUp(Date.Month([Column1]) / 2, 0),
          NOME_MES = Date.MonthName([Column1]),
          NUM_MÊS = Date.Month([Column1]),
          SEMANA_ANO = Date.WeekOfYear([Column1]),
          NOME_DIA = Date.DayOfWeekName([Column1]),
          FIM_MÊS = Date.EndOfMonth([Column1]),
          INICIO_MÊS = Date.EndOfMonth([Column1]),
          ANO_NOME_MÊS = Text.Proper(
            Date.MonthName([Column1]) & " de " & Text.From(Date.Year([Column1]))
          ),
          MES_ANO = Text.Proper(Text.Start(Date.MonthName([Column1]), 3))
            & "-"
            & Text.End(Text.From(Date.Year([Column1])), 2)
        ]
      }
    )
  ),
  _nomes_colunas = Table.AddColumn(Tabela, "Personalizar", each Table.ColumnNames([Outros])){0}[
    Personalizar
  ],
  TabelaCriada = Tabela,
  #"Outros Expandido" = Table.ExpandTableColumn(
    TabelaCriada,
    "Outros",
    _nomes_colunas,
    _nomes_colunas
  ),
  TipoAlterado2 = Table.TransformColumnTypes(
    #"Outros Expandido",
    {
      {"SEMANA_ANO", Int64.Type},
      {"BIMESTRE", Int64.Type},
      {"ANO", Int64.Type},
      {"NOME_MES", type text},
      {"NOME_DIA", type text},
      {"INICIO_MÊS", type date},
      {"FIM_MÊS", type date},
      {"DIA", Int64.Type},
      {"DATA_ID", type text},
      {"ANO_MÊS", type text},
      {"NUM_MÊS", Int64.Type},
      {"ANO_NOME_MÊS", type text},
      {"MES_ANO", type text}
    }
  ),
  Renomear = Table.RenameColumns(TipoAlterado2, {{"Column1", "DATA"}})
in
  Renomear
